plugins {
    id 'com.gradle.plugin-publish' version '0.9.6'
    id 'idea'
    id 'java'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'pl.allegro.tech.build.axion-release' version '1.4.1'
}

group 'uk.bluedawnsolutions.gradle'

// axion-release
project.ext.setProperty('release.disableChecks', 'true')
project.ext.setProperty('release.pushTagsOnly', 'true')
scmVersion {
    tag {
        prefix = project.name
    }
    versionIncrementer 'incrementMinor'
}
project.version = scmVersion.version

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile group: 'io.dropwizard.metrics', name: 'metrics-core', version: '3.1.2'
    compile group: 'io.dropwizard.metrics', name: 'metrics-graphite', version: '3.1.2'


    testCompile gradleTestKit()
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile ('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

gradlePlugin {
    testSourceSets sourceSets.test

    plugins {
        metricsPlugin {
            id = "uk.bluedawnsolutions.gradle.metrics-plugin"
            implementationClass = "uk.bluedawnsolutions.gradle.MetricsPlugin"
        }
    }
}

task wrapper(type: Wrapper) {
    // Do not for any reason downgrade this past 2.13, otherwise we lose the gradle plugin development goodness
    gradleVersion = '2.14.1'
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
    website = 'http://www.gradle.org/'
    vcsUrl = 'https://github.com/kevinpotgieter/gradle-metrics-plugin'
    description = 'A plugin capable of publishing metrics about the time taken for tasks as well as number of build successes and failures.'
    tags = ['build', 'metrics']

    plugins {
        metricsPlugin {
            id = "uk.bluedawnsolutions.gradle.metrics-plugin"
            displayName = 'Gradle Build Metrics'
        }
    }
}